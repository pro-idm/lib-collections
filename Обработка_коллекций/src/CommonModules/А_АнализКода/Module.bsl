#Область ТипыТокенов
// Типы токенов:
// Неизвестный - 0
// ЛеваяСкобка - 1
// ПраваяСкобка - 2
// Точка - 3
// Равно - 4
// ТочкаСЗапятой - 5
// Идентификатор - 6
// Пробелы - 7
// Строка - 8
// КлючевоеСлово - 9
// Функция - 10
// Параметр - 11
// Переменная - 12
// Поле - 13
#КонецОбласти

#Область ЛексическийАнализатор

#Область СостояниеЛексическогоАнализатора
// Состояния лексического анализатора:
//  1 - Начало токена
//  2 - Идентификатор
//  3 - Пробелы
//  4 - Односимвольный токен
//  5 - Строка
//  0 - Неизвестный токен
// -1 - Конец кода

#КонецОбласти //СостояниеЛексическогоАнализатора

Функция ЛексическийАнализ(Знач Код) Экспорт
	Состояние = 1;
	Позиция = 1;
	Токены = Новый Массив;
	Значение = "";
	
	Пока Истина Цикл
		Если Состояние = 1 Тогда
			ОбработатьНачалоТокена(Код, Состояние, Позиция, Значение);
		ИначеЕсли Состояние = 2 Тогда
			ОбработатьИдентификатор(Код, Состояние, Позиция, Токены, Значение);
		ИначеЕсли Состояние = 3 Тогда
			ОбработатьПробелы(Код, Состояние, Позиция, Токены, Значение);
		ИначеЕсли Состояние = 4 Тогда
			ОбработатьОдносимвольныйТокен(Код, Состояние, Позиция, Токены);
		ИначеЕсли Состояние = 5 Тогда
			ОбработатьСтрока(Код, Состояние, Позиция, Токены, Значение);
		ИначеЕсли Состояние = 0 Тогда
			ОбработатьНеизвестныйТокен(Код, Состояние, Позиция, Токены, Значение);
		ИначеЕсли Состояние = -1 Тогда
			Прервать;
		Иначе
			ВызватьИсключение "Неизвестное состояние: " + Состояние;
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат Токены;
КонецФункции

Процедура ОбработатьНачалоТокена(Код, Состояние, Позиция, Значение)
	Символ = ПрочитатьСимвол(Код, Позиция);
	
	Если Символ = "" Тогда
		Состояние = -1;
	ИначеЕсли ЭтоПробел(Символ) Тогда
		Значение = Символ;
		Состояние = 3;
	ИначеЕсли ЭтоОдносимвольныйТокен(Символ) Тогда
		Состояние = 4;
	ИначеЕсли ЭтоНачалоИдентификатора(Символ) Тогда
		Значение = Символ;
		Состояние = 2;
	ИначеЕсли ЭтоСтрока(Символ) Тогда
		Значение = Символ;
		Состояние = 5;
	Иначе
		Значение = Символ;
		Состояние = 0;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьПробелы(Код, Состояние, Позиция, Токены, Значение)
	Позиция = Позиция + 1;
	Символ = ПрочитатьСимвол(Код, Позиция);
	Если Символ <> "" И ЭтоПробел(Символ) Тогда
		Значение = Значение + Символ;
	Иначе		
		Токены.Добавить(Токен(7, Значение));
		Состояние = 1;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьОдносимвольныйТокен(Код, Состояние, Позиция, Токены)
	Символ = ПрочитатьСимвол(Код, Позиция);
	
	ТипТокена = "";
	Если Символ = "(" Тогда
		ТипТокена = 1;
	ИначеЕсли Символ = ")" Тогда 
		ТипТокена = 2;
	ИначеЕсли Символ = "." Тогда 
		ТипТокена = 3;
	ИначеЕсли Символ = "=" Тогда 
		ТипТокена = 4;
	ИначеЕсли Символ = ";" Тогда 
		ТипТокена = 5;
	Иначе
		ВызватьИсключение "Неизвестный односимвольный токен: " + Символ; 
	КонецЕсли; 
	
	Токены.Добавить(Токен(ТипТокена, Символ));
	Позиция = Позиция + 1;
	Состояние = 1;
КонецПроцедуры

Процедура ОбработатьИдентификатор(Код, Состояние, Позиция, Токены, Значение)
	Позиция = Позиция + 1;
	Символ = ПрочитатьСимвол(Код, Позиция);
	Если Символ <> "" И ЭтоСимволИдентификатора(Символ) Тогда
		Значение = Значение + Символ;
	Иначе
		Токены.Добавить(Токен(6, Значение));
		Состояние = 1;
	КонецЕсли; 	
КонецПроцедуры

Процедура ОбработатьСтрока(Код, Состояние, Позиция, Токены, Значение)
	Позиция = Позиция + 1;
	Символ = ПрочитатьСимвол(Код, Позиция);
	Если Символ <> "" И Не ЭтоСтрока(Символ) Тогда
		Значение = Значение + Символ;
	Иначе		
		Значение = Значение + Символ;
		Токены.Добавить(Токен(8, Значение));
		
		Позиция = Позиция + 1;
		Состояние = 1;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьНеизвестныйТокен(Код, Состояние, Позиция, Токены, Значение)
	Позиция = Позиция + 1;
	Символ = ПрочитатьСимвол(Код, Позиция);
	Если Символ <> "" И Не ЭтоПробел(Символ) И Не ЭтоНачалоИдентификатора(Символ)
		 И Не ЭтоОдносимвольныйТокен(Символ) И Не ЭтоСтрока(Символ) Тогда
		Значение = Значение + Символ;
	Иначе		
		Токены.Добавить(Токен(0, Значение));
		Состояние = 1;
	КонецЕсли;
КонецПроцедуры

Функция Токен(Тип, Значение)
	Возврат Новый Структура("Тип, Значение", Тип, Значение);
КонецФункции

Функция ЭтоЦифра(Символ)
	Возврат Найти("1234567890", Символ) > 0;
КонецФункции

Функция ЭтоБуква(Символ)
	Возврат Найти("абвгдеёжзийклмнопрстуфхцчшщъыьэюяabcdefghijklmnopqrstuvwxyz", НРег(Символ)) > 0;
КонецФункции

Функция ЭтоПробел(Символ)
	Возврат Символ = " " Или Символ = Символы.ПС Или Символ = Символы.Таб;
КонецФункции 

Функция ЭтоСтрока(Символ)
	Возврат Символ = """";
КонецФункции

Функция ЭтоНачалоИдентификатора(Символ)
	Возврат ЭтоБуква(Символ) Или Символ = "_";
КонецФункции

Функция ЭтоСимволИдентификатора(Символ)
	Возврат ЭтоБуква(Символ) Или ЭтоЦифра(Символ) Или Символ = "_";
КонецФункции 

Функция ЭтоОдносимвольныйТокен(Символ)
	Возврат Найти("().=;", Символ);
КонецФункции 

Функция ПрочитатьСимвол(Код, Позиция)
	Если Позиция > СтрДлина(Код) Тогда
		Возврат "";
	Иначе
		Возврат Сред(Код, Позиция, 1);
	КонецЕсли;	
КонецФункции 

#КонецОбласти //ЛексическийАнализатор

#Область СинтаксическийАнализатор

#Область СостояниеСинтаксическогоАнализатора
// Состояния синтаксического анализатора
// 1 - Начало выражения
// 2 - Правая часть выражения
// 3 - После точки
// 4 - После ключевого слова "Новый"

#КонецОбласти //СостояниеСинтаксическогоАнализатора

Функция СинтаксическийАнализ(Токены, Параметры) Экспорт
	Состояние = 1;
	Переменные = Новый Массив;
	КлючевыеСлова = ПолучитьКлючевыеСлова();
	ПозицияПоследнегоВыражения = 0;
	
	ВГраница = Токены.ВГраница();
	Для Позиция = 0 По ВГраница Цикл
		ТекущийТокен = Токены[Позиция];
		
		Если ТекущийТокен.Тип = 7 Тогда
			Продолжить;
		КонецЕсли; 
		
		Если Состояние = 1 Тогда
			ПозицияПоследнегоВыражения = Позиция;
			Состояние = ОбработатьВыражение(Параметры, Переменные, Позиция, ТекущийТокен, Токены, КлючевыеСлова);
		ИначеЕсли Состояние = 2 Тогда
			Состояние = ОбработатьВыражение(Параметры, Переменные, Позиция, ТекущийТокен, Токены, КлючевыеСлова, Ложь);
		ИначеЕсли Состояние = 3 Тогда
			Состояние = ОбработатьПослеТочки(Позиция, ТекущийТокен, Токены);
		ИначеЕсли Состояние = 4 Тогда
			Состояние = ОбработатьПослеНовый(ТекущийТокен, Токены);
		Иначе
			ВызватьИсключение "Неизвестное состояние: " + Состояние;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат ПозицияПоследнегоВыражения;
КонецФункции

Функция ОбработатьВыражение(Параметры, Переменные, Позиция, ТекущийТокен, Токены, КлючевыеСлова, ЛеваяЧасть = Истина)
	Если ТекущийТокен.Тип = 6 Тогда
		Если ЭтоКлючевоеСлово(КлючевыеСлова, ТекущийТокен.Значение) Тогда
			ТекущийТокен.Тип = 9;
			Если ЭтоКлючевоеСловоСВложеннымВыражением(ТекущийТокен.Значение) Тогда
				Состояние = 1;
			ИначеЕсли ЭтоКлючевоеСловоНовый(ТекущийТокен.Значение) Тогда
				Состояние = 4;			
			Иначе
				Состояние = 2;
			КонецЕсли; 
		ИначеЕсли ПроверитьТипСледующегоТокена(Токены, Позиция + 1 , 1) Тогда
			ТекущийТокен.Тип = 10;
			Состояние = 2;
		ИначеЕсли ЭтоПараметр(Параметры, ТекущийТокен) Тогда
			ТекущийТокен.Тип = 11;
			Состояние = 2;
		ИначеЕсли ЭтоПеременная(Переменные, ТекущийТокен.Значение) Тогда 
			ТекущийТокен.Тип = 12;
			Состояние = 2;
		ИначеЕсли ПроверитьТипСледующегоТокена(Токены, Позиция + 1, 4) И ЛеваяЧасть Тогда
			ТекущийТокен.Тип = 12;
			Переменные.Добавить(ТекущийТокен.Значение);
			Состояние = 2;
		Иначе
			Состояние = 2;
		КонецЕсли;
	ИначеЕсли ТекущийТокен.Тип = 3 Тогда 
		Состояние = 3;
	ИначеЕсли ТекущийТокен.Тип = 5 Тогда 
		Состояние = 1;
	Иначе
		Состояние = 2;
	КонецЕсли;
	
	Возврат Состояние;
КонецФункции

Функция ОбработатьПослеТочки(Позиция, ТекущийТокен, Токены)
	Если ТекущийТокен.Тип = 6 Тогда
		Если ПроверитьТипСледующегоТокена(Токены, Позиция + 1 , 1) Тогда
			ТекущийТокен.Тип = 10;
		Иначе
			ТекущийТокен.Тип = 13;
		КонецЕсли;
	КонецЕсли;
	
	Состояние = 2;
	Возврат Состояние;	
КонецФункции

Функция ОбработатьПослеНовый(ТекущийТокен, Токены)
	Если ТекущийТокен.Тип = 6 Тогда
		ТекущийТокен.Тип = 10;
	КонецЕсли;
	
	Состояние = 2;
	Возврат Состояние;
КонецФункции

Функция ПроверитьТипСледующегоТокена(Токены, Позиция, Тип)
	Результат = Ложь;
	Для ТекПозиция = Позиция По Токены.ВГраница() Цикл
		Токен = Токены[ТекПозиция];
		Если Токен.Тип <> 7 Тогда
			Результат = Токен.Тип = Тип;
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат Результат;
КонецФункции 

Функция ПолучитьКлючевыеСлова()
	КлючевыеСлова = Новый Массив;
	КлючевыеСлова.Добавить("новый");
	КлючевыеСлова.Добавить("неопределено");	
	КлючевыеСлова.Добавить("null");
	КлючевыеСлова.Добавить("истина");
	КлючевыеСлова.Добавить("ложь");
	КлючевыеСлова.Добавить("и");
	КлючевыеСлова.Добавить("или");
	КлючевыеСлова.Добавить("не");
	КлючевыеСлова.Добавить("если");
	КлючевыеСлова.Добавить("тогда");
	КлючевыеСлова.Добавить("иначеесли");
	КлючевыеСлова.Добавить("иначе");
	КлючевыеСлова.Добавить("конецесли");
	КлючевыеСлова.Добавить("пока");
	КлючевыеСлова.Добавить("для");
	КлючевыеСлова.Добавить("по");
	КлючевыеСлова.Добавить("каждого");
	КлючевыеСлова.Добавить("цикл");
	КлючевыеСлова.Добавить("прервать");
	КлючевыеСлова.Добавить("продолжить");
	КлючевыеСлова.Добавить("конеццикла");
	КлючевыеСлова.Добавить("вызватьисключение");

	Возврат КлючевыеСлова;
КонецФункции 

Функция ЭтоКлючевоеСлово(КлючевыеСлова, Значение)
	Возврат КлючевыеСлова.Найти(НРег(Значение)) <> Неопределено;
КонецФункции

Функция ЭтоПараметр(Параметры, ТекущийТокен)
	Возврат Параметры.Найти(ТекущийТокен.Значение) <> Неопределено;
КонецФункции

Функция ЭтоПеременная(Переменные, Значение);
	Возврат Переменные.Найти(Значение) <> Неопределено;
КонецФункции

Функция ЭтоКлючевоеСловоСВложеннымВыражением(Значение)
	КлючевоеСлово = НРег(Значение);
	Возврат КлючевоеСлово = "тогда" Или КлючевоеСлово = "иначе"
				 Или КлючевоеСлово = "цикл" Или КлючевоеСлово = "для";
КонецФункции

Функция ЭтоКлючевоеСловоНовый(Значение)
	Возврат НРег(Значение) = "новый";
КонецФункции
 
#КонецОбласти //СинтаксическийАнализатор

#Область ОбработкаКода

Функция ДобавитьПолучениеРезультата(Код, ПеременнаяРезультат) Экспорт
	Токены = ЛексическийАнализ(Код);
	ПозицияПоследнегоВыражения = СинтаксическийАнализ(Токены, Новый Массив);
	ДобавитьПрисвоениеРезультата(ПозицияПоследнегоВыражения, Токены, ПеременнаяРезультат);
	
	Возврат ПолучитьКод(Токены);
КонецФункции

Функция ПодготовитьКодДляВстраивания(Код, Замены, Префикс = "", ПеременнаяРезультат = "") Экспорт
	Параметры = ПолучитьКлючиСтруктуры(Замены);
	
	Токены = ЛексическийАнализ(Код);
	ПозицияПоследнегоВыражения = СинтаксическийАнализ(Токены, Параметры);
	
	Для Каждого Токен Из Токены Цикл
		Если Токен.Тип = 11 Тогда
			Токен.Значение = Замены[Токен.Значение];
		ИначеЕсли Токен.Тип = 12 И Не ПустаяСтрока(Префикс) Тогда
			Токен.Значение = Префикс + "_" + Токен.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ПустаяСтрока(ПеременнаяРезультат) Тогда
		ДобавитьПрисвоениеРезультата(ПозицияПоследнегоВыражения, Токены, ПеременнаяРезультат);
	КонецЕсли; 
	
	ЗакончитьВыражениеЕслиНеобходимо(Токены);
	
	Возврат ПолучитьКод(Токены);
КонецФункции

Процедура ЗакончитьВыражениеЕслиНеобходимо(Токены)
	Если ПолучитьТипПервогоТокенаСКонца(Токены) <> 5 Тогда
		Токены.Добавить(Токен(5, ";"));
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьТипПервогоТокенаСКонца(Токены)
	Тип = Неопределено;
	
	ТекПозиция = Токены.ВГраница();
	Пока ТекПозиция >= 0 Цикл
		Токен = Токены[ТекПозиция];
		Если Токен.Тип <> 7 Тогда
			Тип = Токен.Тип;
			Прервать;
		КонецЕсли;
		ТекПозиция = ТекПозиция - 1;
	КонецЦикла;
	
	Возврат Тип;
КонецФункции

Процедура ДобавитьПрисвоениеРезультата(ПозицияПоследнегоВыражения, Токены, ПеременнаяРезультат)
	Токены.Вставить(ПозицияПоследнегоВыражения, Токен(4, "="));
	Токены.Вставить(ПозицияПоследнегоВыражения, Токен(12, ПеременнаяРезультат));
КонецПроцедуры
  
Функция ПолучитьКод(Токены)
	Код = "";
	Для Каждого Токен Из Токены Цикл
		Код = Код + Токен.Значение;
	КонецЦикла; 
	
	Возврат Код;
КонецФункции

Функция ПолучитьКлючиСтруктуры(Структура)
	Ключи = Новый Массив;
	Для Каждого КлючЗначение Из Структура Цикл
		Ключи.Добавить(КлючЗначение.Ключ);
	КонецЦикла; 
	
	Возврат Ключи;
КонецФункции
 
#КонецОбласти //ОбработкаКода