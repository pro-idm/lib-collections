#Область СостояниеЛексическогоАнализатора
// Состояния лексического анализатора:
//  1 - Начало токена
//  2 - Идентификатор
//  3 - Пробелы
//  4 - Односимвольный токен
//  5 - Строка
//  0 - Неизвестный токен
// -1 - Конец кода
Перем ЛА_Состояние;
Перем ЛА_Код;
Перем ЛА_Позиция;
Перем ЛА_Символ;
Перем ЛА_Значение;
Перем ЛА_Токены;

#КонецОбласти //СостояниеЛексическогоАнализатора

#Область СостояниеСинтаксическогоАнализатора
// Состояния синтаксического анализатора
// 1 - Начало выражения
// 2 - Правая часть выражения
// 3 - После точки
Перем СА_Состояние;
Перем СА_Токены;
Перем СА_Позиция;
Перем СА_Переменные;
Перем СА_КлючевыеСлова;
Перем СА_ПозицияВыражения;
#КонецОбласти //СостояниеСинтаксическогоАнализатора

#Область ТипыТокенов
// Типы токенов:
// Неизвестный - 0
// ЛеваяСкобка - 1
// ПраваяСкобка - 2
// Точка - 3
// Равно - 4
// ТочкаСЗапятой - 5
// Идентификатор - 6
// Пробелы - 7
// Строка - 8
// КлючевоеСлово - 9
// Функция - 10
// Параметр - 11
// Переменная - 12
// Поле - 13
#КонецОбласти

#Область ЛексическийАнализатор

Функция ЛексическийАнализ(Знач Код) Экспорт
	ЛА_Состояние = 1;
	ЛА_Позиция = 1;
	ЛА_Токены = Новый Массив;
	ЛА_Код = Код;
	ЛА_Символ = "";
	ЛА_Значение = "";
	
	Пока Истина Цикл
		Если ЛА_Состояние = 1 Тогда
			ОбработатьНачалоТокена();
		ИначеЕсли ЛА_Состояние = 2 Тогда
			ОбработатьИдентификатор();
		ИначеЕсли ЛА_Состояние = 3 Тогда
			ОбработатьПробелы();
		ИначеЕсли ЛА_Состояние = 4 Тогда
			ОбработатьОдносимвольныйТокен();
		ИначеЕсли ЛА_Состояние = 5 Тогда
			ОбработатьСтрока();
		ИначеЕсли ЛА_Состояние = 0 Тогда
			ОбработатьНеизвестныйТокен();
		ИначеЕсли ЛА_Состояние = -1 Тогда
			Прервать;
		Иначе
			ВызватьИсключение "Неизвестное состояние: " + ЛА_Состояние;
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат ЛА_Токены;
КонецФункции

Процедура ОбработатьНачалоТокена()
	ЛА_Символ = ПрочитатьСимвол(ЛА_Код, ЛА_Позиция);
	
	Если ЛА_Символ = "" Тогда
		ЛА_Состояние = -1;
	ИначеЕсли ЭтоПробел(ЛА_Символ) Тогда
		ЛА_Значение = ЛА_Символ;
		ЛА_Состояние = 3;
	ИначеЕсли ЭтоОдносимвольныйТокен(ЛА_Символ) Тогда
		ЛА_Состояние = 4;
	ИначеЕсли ЭтоНачалоИдентификатора(ЛА_Символ) Тогда
		ЛА_Значение = ЛА_Символ;
		ЛА_Состояние = 2;
	ИначеЕсли ЭтоСтрока(ЛА_Символ) Тогда
		ЛА_Значение = ЛА_Символ;
		ЛА_Состояние = 5;
	Иначе
		ЛА_Значение = ЛА_Символ;
		ЛА_Состояние = 0;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьПробелы()
	ЛА_Позиция = ЛА_Позиция + 1;
	ЛА_Символ = ПрочитатьСимвол(ЛА_Код, ЛА_Позиция);
	Если ЛА_Символ <> "" И ЭтоПробел(ЛА_Символ) Тогда
		ЛА_Значение = ЛА_Значение + ЛА_Символ;
	Иначе		
		ЛА_Токены.Добавить(Токен(7, ЛА_Значение));
		ЛА_Состояние = 1;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьОдносимвольныйТокен()
	ТипТокена = "";
	Если ЛА_Символ = "(" Тогда
		ТипТокена = 1;
	ИначеЕсли ЛА_Символ = ")" Тогда 
		ТипТокена = 2;
	ИначеЕсли ЛА_Символ = "." Тогда 
		ТипТокена = 3;
	ИначеЕсли ЛА_Символ = "=" Тогда 
		ТипТокена = 4;
	ИначеЕсли ЛА_Символ = ";" Тогда 
		ТипТокена = 5;
	Иначе
		ВызватьИсключение "Неизвестный односимвольный токен: " + ЛА_Символ; 
	КонецЕсли; 
	
	ЛА_Токены.Добавить(Токен(ТипТокена, ЛА_Символ));
	ЛА_Позиция = ЛА_Позиция + 1;
	ЛА_Состояние = 1;
КонецПроцедуры

Процедура ОбработатьИдентификатор()
	ЛА_Позиция = ЛА_Позиция + 1;
	ЛА_Символ = ПрочитатьСимвол(ЛА_Код, ЛА_Позиция);
	Если ЛА_Символ <> "" И ЭтоСимволИдентификатора(ЛА_Символ) Тогда
		ЛА_Значение = ЛА_Значение + ЛА_Символ;
	Иначе
		ЛА_Токены.Добавить(Токен(6, ЛА_Значение));
		ЛА_Состояние = 1;
	КонецЕсли; 	
КонецПроцедуры

Процедура ОбработатьСтрока()
	ЛА_Позиция = ЛА_Позиция + 1;
	ЛА_Символ = ПрочитатьСимвол(ЛА_Код, ЛА_Позиция);
	Если ЛА_Символ <> "" И Не ЭтоСтрока(ЛА_Символ) Тогда
		ЛА_Значение = ЛА_Значение + ЛА_Символ;
	Иначе		
		ЛА_Значение = ЛА_Значение + ЛА_Символ;
		ЛА_Токены.Добавить(Токен(8, ЛА_Значение));
		
		ЛА_Позиция = ЛА_Позиция + 1;
		ЛА_Состояние = 1;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьНеизвестныйТокен()
	ЛА_Позиция = ЛА_Позиция + 1;
	ЛА_Символ = ПрочитатьСимвол(ЛА_Код, ЛА_Позиция);
	Если ЛА_Символ <> "" И Не ЭтоПробел(ЛА_Символ) И Не ЭтоНачалоИдентификатора(ЛА_Символ)
		 И Не ЭтоОдносимвольныйТокен(ЛА_Символ) И Не ЭтоСтрока(ЛА_Символ) Тогда
		ЛА_Значение = ЛА_Значение + ЛА_Символ;
	Иначе		
		ЛА_Токены.Добавить(Токен(0, ЛА_Значение));
		ЛА_Состояние = 1;
	КонецЕсли;
КонецПроцедуры

Функция Токен(Тип, Значение)
	Возврат Новый Структура("Тип, Значение", Тип, Значение);
КонецФункции

Функция ЭтоЦифра(Символ)
	Возврат Найти("1234567890", Символ) > 0;
КонецФункции

Функция ЭтоБуква(Символ)
	Возврат Найти("абвгдеёжзийклмнопрстуфхцчшщъыьэюяabcdefghijklmnopqrstuvwxyz", НРег(Символ)) > 0;
КонецФункции

Функция ЭтоПробел(Символ)
	Возврат Символ = " " Или Символ = Символы.ПС Или Символ = Символы.Таб;
КонецФункции 

Функция ЭтоСтрока(Символ)
	Возврат Символ = """";
КонецФункции

Функция ЭтоНачалоИдентификатора(Символ)
	Возврат ЭтоБуква(Символ) Или Символ = "_";
КонецФункции

Функция ЭтоСимволИдентификатора(Символ)
	Возврат ЭтоБуква(Символ) Или ЭтоЦифра(Символ) Или Символ = "_";
КонецФункции 

Функция ЭтоОдносимвольныйТокен(Символ)
	Возврат Найти("().=;", Символ);
КонецФункции 

Функция ПрочитатьСимвол(Код, Позиция)
	Если Позиция > СтрДлина(Код) Тогда
		Возврат "";
	Иначе
		Возврат Сред(Код, Позиция, 1);
	КонецЕсли;	
КонецФункции 

#КонецОбласти //ЛексическийАнализатор

#Область СинтаксическийАнализатор

Процедура СинтаксическийАнализ(Токены, Параметры) Экспорт
	СА_Переменные = Новый Массив;
	СА_Состояние = 1;
	СА_Токены = Токены;
	
	Контекст = Новый Структура;
	Контекст.Вставить("Параметры", Параметры);
	
	ТокеныВГраница = Токены.ВГраница();
	Для СА_Позиция = 0 По ТокеныВГраница Цикл
		ТекущийТокен = Токены[СА_Позиция];
		
		Если ТекущийТокен.Тип = 7 Тогда
			Продолжить;
		КонецЕсли; 
		
		Если СА_Состояние = 1 Тогда
			СА_ПозицияВыражения = СА_Позиция;
			ОбработатьВыражение(Контекст, ТекущийТокен, Токены);
		ИначеЕсли СА_Состояние = 2 Тогда
			ОбработатьВыражение(Контекст, ТекущийТокен, Токены, Ложь);
		ИначеЕсли СА_Состояние = 3 Тогда
			ОбработатьПослеТочки(ТекущийТокен, Токены);
		Иначе
			ВызватьИсключение "Неизвестное состояние: " + СА_Состояние;
		КонецЕсли; 
	КонецЦикла;
КонецПроцедуры

Процедура ОбработатьВыражение(Контекст, ТекущийТокен, Токены, ЛеваяЧасть = Истина)
	Если ТекущийТокен.Тип = 6 Тогда
		Если ЭтоКлючевоеСлово(ТекущийТокен.Значение) Тогда
			ТекущийТокен.Тип = 9;
			Если ЭтоКлючевоеСловоСВложеннымВыражением(ТекущийТокен.Значение) Тогда
				СА_Состояние = 1;
			Иначе
				СА_Состояние = 2;
			КонецЕсли; 
		ИначеЕсли ПроверитьТипСледующегоТокена(Токены, СА_Позиция + 1 , 1) Тогда
			ТекущийТокен.Тип = 10;
			СА_Состояние = 2;
		ИначеЕсли ЭтоПараметр(Контекст, ТекущийТокен) Тогда
			ТекущийТокен.Тип = 11;
			СА_Состояние = 2;
		ИначеЕсли ЭтоПеременная(ТекущийТокен.Значение) Тогда 
			ТекущийТокен.Тип = 12;
			СА_Состояние = 2;
		ИначеЕсли ПроверитьТипСледующегоТокена(Токены, СА_Позиция + 1 , 4) И ЛеваяЧасть Тогда
			ТекущийТокен.Тип = 12;
			СА_Переменные.Добавить(ТекущийТокен.Значение);
			СА_Состояние = 2;
		Иначе
			СА_Состояние = 2;
		КонецЕсли;
	ИначеЕсли ТекущийТокен.Тип = 3 Тогда 
		СА_Состояние = 3;
	ИначеЕсли ТекущийТокен.Тип = 5 Тогда 
		СА_Состояние = 1;
	Иначе
		СА_Состояние = 2;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьПослеТочки(ТекущийТокен, Токены)
	Если ТекущийТокен.Тип = 6 Тогда
		Если ПроверитьТипСледующегоТокена(Токены, СА_Позиция + 1 , 1) Тогда
			ТекущийТокен.Тип = 10;
		Иначе
			ТекущийТокен.Тип = 13;
		КонецЕсли;
	КонецЕсли;
	СА_Состояние = 2;	
КонецПроцедуры

Функция ПроверитьТипСледующегоТокена(Токены, Позиция, Тип)
	Результат = Ложь;
	Для ТекПозиция = Позиция По Токены.ВГраница() Цикл
		Токен = Токены[ТекПозиция];
		Если Токен.Тип <> 7 Тогда
			Результат = Токен.Тип = Тип;
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат Результат;
КонецФункции

Функция ПолучитьТипПервогоТокенаСКонца(Токены)
	Тип = Неопределено;
	
	ТекПозиция = Токены.ВГраница();
	Пока ТекПозиция >= 0 Цикл
		Токен = Токены[ТекПозиция];
		Если Токен.Тип <> 7 Тогда
			Тип = Токен.Тип;
			Прервать;
		КонецЕсли;
		ТекПозиция = ТекПозиция - 1;
	КонецЦикла;
	
	Возврат Тип;
КонецФункции 

Функция ПолучитьКлючевыеСлова()
	СА_КлючевыеСлова = Новый Массив;
	СА_КлючевыеСлова.Добавить("неопределено");	
	СА_КлючевыеСлова.Добавить("null");
	СА_КлючевыеСлова.Добавить("истина");
	СА_КлючевыеСлова.Добавить("ложь");
	СА_КлючевыеСлова.Добавить("и");
	СА_КлючевыеСлова.Добавить("или");
	СА_КлючевыеСлова.Добавить("не");
	СА_КлючевыеСлова.Добавить("если");
	СА_КлючевыеСлова.Добавить("тогда");
	СА_КлючевыеСлова.Добавить("иначеесли");
	СА_КлючевыеСлова.Добавить("иначе");
	СА_КлючевыеСлова.Добавить("конецесли");
	СА_КлючевыеСлова.Добавить("пока");
	СА_КлючевыеСлова.Добавить("для");
	СА_КлючевыеСлова.Добавить("по");
	СА_КлючевыеСлова.Добавить("каждого");
	СА_КлючевыеСлова.Добавить("цикл");
	СА_КлючевыеСлова.Добавить("прервать");
	СА_КлючевыеСлова.Добавить("продолжить");
	СА_КлючевыеСлова.Добавить("конеццикла");
	СА_КлючевыеСлова.Добавить("вызватьисключение");

	Возврат СА_КлючевыеСлова;
КонецФункции 

Функция ЭтоКлючевоеСлово(Значение)
	Возврат СА_КлючевыеСлова.Найти(НРег(Значение)) <> Неопределено;
КонецФункции

Функция ЭтоПараметр(Контекст, ТекущийТокен)
	Возврат Контекст.Параметры.Найти(ТекущийТокен.Значение) <> Неопределено;
КонецФункции

Функция ЭтоПеременная(Значение);
	Возврат СА_Переменные.Найти(Значение) <> Неопределено;
КонецФункции

Функция ЭтоКлючевоеСловоСВложеннымВыражением(Значение)
	КлючевоеСлово = НРег(Значение);
	Возврат КлючевоеСлово = "тогда" Или КлючевоеСлово = "иначе"
				 Или КлючевоеСлово = "цикл" Или КлючевоеСлово = "для";
КонецФункции
 
#КонецОбласти //СинтаксическийАнализатор

#Область ОбработкаКода

Функция ДобавитьПолучениеРезультата(Код, ПеременнаяРезультат) Экспорт
	Токены = ЛексическийАнализ(Код);
	СинтаксическийАнализ(Токены, Новый Массив);
	ДобавитьПрисвоениеРезультата(Токены, ПеременнаяРезультат);
	
	Возврат ПолучитьКод(Токены);
КонецФункции

Функция ПодготовитьКодДляВстраивания(Код, Замены, Префикс = "", ПеременнаяРезультат = "") Экспорт
	Параметры = ПолучитьКлючиСтруктуры(Замены);
	
	Токены = ЛексическийАнализ(Код);
	СинтаксическийАнализ(Токены, Параметры);
	
	Для Каждого Токен Из Токены Цикл
		Если Токен.Тип = 11 Тогда
			Токен.Значение = Замены[Токен.Значение];
		ИначеЕсли Токен.Тип = 12 И Не ПустаяСтрока(Префикс) Тогда
			Токен.Значение = Префикс + "_" + Токен.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ПустаяСтрока(ПеременнаяРезультат) Тогда
		ДобавитьПрисвоениеРезультата(Токены, ПеременнаяРезультат);
	КонецЕсли; 
	
	ЗакончитьВыражениеЕслиНеобходимо(Токены);
	
	Возврат ПолучитьКод(Токены);
КонецФункции

Процедура ЗакончитьВыражениеЕслиНеобходимо(Токены)
	Если ПолучитьТипПервогоТокенаСКонца(Токены) <> 5 Тогда
		Токены.Добавить(Токен(5, ";"));
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьПрисвоениеРезультата(Токены, ПеременнаяРезультат)
	Токены.Вставить(СА_ПозицияВыражения, Токен(4, "="));
	Токены.Вставить(СА_ПозицияВыражения, Токен(12, ПеременнаяРезультат));
КонецПроцедуры
  
Функция ПолучитьКод(Токены)
	Код = "";
	Для Каждого Токен Из Токены Цикл
		Код = Код + Токен.Значение;
	КонецЦикла; 
	
	Возврат Код;
КонецФункции

Функция ПолучитьКлючиСтруктуры(Структура)
	Ключи = Новый Массив;
	Для Каждого КлючЗначение Из Структура Цикл
		Ключи.Добавить(КлючЗначение.Ключ);
	КонецЦикла; 
	
	Возврат Ключи;
КонецФункции
 
#КонецОбласти //ОбработкаКода

#Область Инициализация

СА_КлючевыеСлова = ПолучитьКлючевыеСлова();

#КонецОбласти //Инициализация